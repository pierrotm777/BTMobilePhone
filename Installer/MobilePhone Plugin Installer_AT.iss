; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; EDIT THESE LINES
#define RRExtPluginName "MobilePhone"
#define RRExtPluginFriendlyName "BTMobilePhone (AT commands used)"
#define RRExtPluginVersion "V1.1.7.2"
#define RRExtPluginPublisher "A. Fernando,C. Champion, P. Montet"
#define RRExtURL "http://www.mp3car.com/rr-plugins/154943-bt-mobilephone-plugin.html#post1487622"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A5DD203A-A2AD-437C-B9AA-AAEBF1D13D2D}
AppName=RR Plugin for {#RRExtPluginFriendlyName}
AppVerName=RR Plugin for {#RRExtPluginFriendlyName} {#RRExtPluginVersion}
AppPublisher={#RRExtPluginPublisher}
AppPublisherURL={#RRExtURL}
AppSupportURL={#RRExtURL}
AppUpdatesURL={#RRExtURL}
DefaultDirName={reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}\Plugins\{#RRExtPluginName}
OutputDir=..\Installer Output
OutputBaseFilename=RR Plugin {#RRExtPluginFriendlyName} {#RRExtPluginVersion} Setup
SourceDir=Source_AT
Compression=lzma
SolidCompression=yes
Uninstallable=yes
;LicenseFile=License.txt

[Languages]
Name: en;    MessagesFile: "compiler:Default.isl"
Name: fr;   MessagesFile: "compiler:Languages\French.isl"
Name: de;    MessagesFile: "compiler:Languages\German.isl"
Name: fi;    MessagesFile: "compiler:Languages\Finnish.isl"
Name: it;    MessagesFile: "compiler:Languages\Italian.isl"
Name: pl;    MessagesFile: "compiler:Languages\Polish.isl"
Name: pt_br; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: ru;    MessagesFile: "compiler:Languages\Russian.isl"

#include <idp.iss>
; Language files must be included after idp.iss and after [Languages] section
#include <idplang\French.iss>
#include <idplang\German.iss>
#include <idplang\Finnish.iss>
#include <idplang\Italian.iss>
#include <idplang\Polish.iss>
#include <idplang\BrazilianPortuguese.iss>
#include <idplang\Russian.iss>

; Let's change some of standard strings:
[CustomMessages]
en.IDP_FormCaption=Downloading lot of files...
fr.IDP_FormCaption=Téléchargement de plusieurs fichiers...
de.IDP_FormCaption=Upload mehrerer Dateien
fi.IDP_FormCaption=Lataaminen useita tiedostoja
it.IDP_FormCaption=Caricamento di più file
pl.IDP_FormCaption=Przesyłanie wielu plików
pt_br.IDP_FormCaption=Carregando múltiplos arquivos
ru.IDP_FormCaption=Загрузка нескольких файлов

[Tasks]
Name: "SSCDL"; Description: "Install the Sample Skin into Carwings Dynamic Lite"; Languages: de en fi fr it pl pt_br ru
Name: "SSCDL2"; Description: "Install the Sample Skin into DFX5"; Languages: de en fi fr it pl pt_br ru
Name: "SSCDL3"; Description: "Install the Sample Skin into Reborn"; Languages: de en fi fr it pl pt_br ru
Name: "SSCDL4"; Description: "Install the Sample Skin into eLite2_800x600"; Languages: de en fi fr it pl pt_br ru

Name: "SSCDL5"; Description: "Copy RR Plugin GpsLockout install"; Languages: de en fi fr it pl pt_br ru
;Name: "SSCDL6"; Description: "Install the Sample Skin into Carwings Dynamic Lite"; Languages: de en fi fr it pl pt_br ru
;Name: "SSCDL7"; Description: "Install the Sample Skin into DFX5"; Languages: de en fi fr it pl pt_br ru
;Name: "SSCDL8"; Description: "Install the Sample Skin into Reborn"; Languages: de en fi fr it pl pt_br ru
;Name: "SSCDL9"; Description: "Install the Sample Skin into eLite 2"; Languages: de en fi fr it pl pt_br ru

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files 
Source: "MobilePhone.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Readme.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "MobilePhone Skin Commands.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "SlideShow.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "MobilePhone_ATCMD.lst"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{tmp}\RR Plugin GpsLockout Setup.zip"; DestDir: "{app}"; Flags: external; ExternalSize: 1048576; Permissions: users-modify; Tasks: SSCDL5


;Tools
Source: "Tools\*"; DestDir: "{app}\Tools"; Flags: ignoreversion recursesubdirs createallsubdirs

; PhoneBook
;Source: "PhoneBook\*"; DestDir: "{app}\PhoneBook"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Photo\*"; DestDir: "{app}\Photo"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Models\*"; DestDir: "{app}\Models"; Flags: ignoreversion recursesubdirs createallsubdirs

; main skin 
Source: "Sample Skin_AT\*"; DestDir: "{app}\Sample Skin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Sample Skin_AT\Carwings_Dynamic_Lite\*"; DestDir: "{code:DataPath}\Skins\Carwings_Dynamic_Lite"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL
Source: "Sample Skin_AT\DFX5\*"; DestDir: "{code:DataPath}\Skins\DFX5"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL2
Source: "Sample Skin_AT\Reborn\*"; DestDir: "{code:DataPath}\Skins\Reborn"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL3
Source: "Sample Skin_AT\eLite2_800x600\*"; DestDir: "{code:DataPath}\Skins\eLite2_800x600"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL4

;GpsLockOut skin
;Source: "GpsLockout\Carwings_Dynamic_Lite\*"; DestDir: "{code:DataPath}\Skins\Carwings_Dynamic_Lite"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL6
;Source: "GpsLockout\DFX5\*"; DestDir: "{code:DataPath}\Skins\DFX5"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL7
;Source: "GpsLockout\Reborn\*"; DestDir: "{code:DataPath}\Skins\Reborn"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL8
;Source: "GpsLockout\eLite 2\*"; DestDir: "{code:DataPath}\Skins\eLite 2"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL9

[Run]
; Regasm
Filename: {dotnet20}\Regasm.exe; Parameters: /tlb /codebase MobilePhone.dll; WorkingDir: {app}; StatusMsg: Registering Plugin...; Flags: runhidden


[UninstallRun]
; Regasm
Filename: {dotnet20}\Regasm.exe; Parameters: /u /tlb MobilePhone.dll; WorkingDir: {app}; StatusMsg: Un-Registering Plugin...; Flags: runhidden


[UninstallDelete]
Name: {app}\MobilePhone.tlb; Type: files
Name: {app}\SlideShow.exe; Type: files
;Name: {tmp}\RR Plugin GpsLockout Setup.zip; Type: files

[Code]


//////////////////////////////////////////////////////////////
// Get PLUG-IN Installation Path, based on Profile Mode
//////////////////////////////////////////////////////////////
function InstallPath(p: String): String;
begin
	// look for rr.ini?
	if FileExists(ExpandConstant('{userdocs}\RideRunner\Config\rr.ini')) then
	begin
		if DirExists(ExpandConstant('{userdocs}\RideRunner\Plugins')) then
			Result := ExpandConstant('{userdocs}\RideRunner\Plugins\{#RRExtPluginName}')
		else
			Result := ExpandConstant('{app}')
	end
	else
		Result := ExpandConstant('{app}')
end;


//////////////////////////////////////////////////////////////
// Get DATA Path, based on Profile Mode
//////////////////////////////////////////////////////////////
function DataPath(p: String): String;
begin
	// look for rr.ini?
	if FileExists(ExpandConstant('{userdocs}\RideRunner\Config\rr.ini')) then
	begin
		if DirExists(ExpandConstant('{userdocs}\RideRunner')) then
			Result := ExpandConstant('{userdocs}\RideRunner')
		else
			Result := ExpandConstant('{reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}')
	end
	else
		Result := ExpandConstant('{reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}')
end;
